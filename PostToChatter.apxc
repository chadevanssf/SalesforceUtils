/*
 * Patterned after: https://developer.salesforce.com/docs/atlas.en-us.apexcode.meta/apexcode/apex_classes_annotation_InvocableVariable.htm
 * Chatter with file: https://salesforce.stackexchange.com/questions/117659/upload-a-file-to-a-chatter-feed-in-apex-api-36
 */
global class PostToChatter {
    
    @InvocableMethod(label='Post to Chatter with an Existing File')
    public static List<PostChatterWithFileResult> postWithExistingFile(List<PostChatterWithFileRequest> requests)
    {
        List<PostChatterWithFileResult> results = new List<PostChatterWithFileResult>();
        for (PostChatterWithFileRequest request : requests)
        {
            // Insert a simple post
            FeedItem post = new FeedItem();
            post.body = request.message;
            if (request.recordId != null)
            {
                post.ParentID = request.recordId;
            } 
            else {
                post.ParentID = UserInfo.getUserID();
            }
            insert post;
            
            // grab the ContentVersion from the ContentDocument
            List<ContentVersion> versions = [SELECT Id
                                             FROM ContentVersion
                                             WHERE ContentDocumentId = :request.fileId
                                             AND islatest = true
                                             LIMIT 1];
            ContentVersion version = versions[0];
            
            // Associate the file with the post
            FeedAttachment postAttachment = new FeedAttachment();
            postAttachment.FeedEntityId = post.id;
            postAttachment.Type = 'Content';
            postAttachment.RecordId = version.Id;
            insert postAttachment;
            
            PostChatterWithFileResult result = new PostChatterWithFileResult();
            result.postId = post.id;
            result.success = true;
            results.add(result);
            
        }
        return results;
    }
    
    global class PostChatterWithFileRequest {
        @InvocableVariable(required=false)
        global ID recordId;
        
        @InvocableVariable(required=true)
        global ID fileId;
        
        @InvocableVariable(required=true)
        global String message;
    }
    
    global class PostChatterWithFileResult {
        @InvocableVariable
        global ID postId;
        
        @InvocableVariable
        global Boolean success;
    }
    
    class PostChatterWithFileException extends Exception {}
}
